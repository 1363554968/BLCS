apply plugin: 'com.android.application'

def keystoreFilepath = ""
def keystorePSW = ""
def keystoreAlias = ""
def keystoreAliasPSW = ""
def keyfile = file('s.keystore.temp')
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath =properties.getProperty("keystore.path")

if(keystoreFilepath){
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}


android {
    signingConfigs {
        release {
            keyAlias keystoreAlias
            keyPassword keystorePSW
            storeFile keyfile
            storePassword keystoreAliasPSW
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "blcs.lwb.utils"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.1"
        flavorDimensions "versionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled true
            shrinkResources true   //排除没有引用到的资源  APK瘦身
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
//    多渠道打包
    productFlavors {
        wandoujia {}
        xiaomi {}
        pugongying {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    //    修改命名规则
    applicationVariants.all { variant ->
        variant.outputs.all {
            def formattedDate = new Date().format('yyyy_MM_dd')
            outputFileName = rootProject.getName() + "-" + variant.flavorName + "-" + buildType.name + "-" + formattedDate + "-v" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk";
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    // ========基本关联=======
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // ========关联工具=======
    implementation project(':LwbTool')
    // ========关联butterknife=======
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //==============友盟================
    implementation 'com.umeng.sdk:common:latest.integration'
    implementation 'com.umeng.sdk:analytics:latest.integration'
    // ========glide=======
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
}
